apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'

android {
    namespace "com.growthbeat"
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION
    publishNonDefault true

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation group: 'androidx.appcompat', name: 'appcompat', version: "$SUPPORT_LIBRARY_VERSION"
    implementation(group: "com.google.firebase", name: "firebase-messaging", version: "$FIREBASE_LIBRARY_VERSION") {
        exclude group: 'com.android.support'
    }
    implementation group: "com.google.android.gms", name:"play-services-ads-identifier", version: "$PLAY_SERVICE_AD_ID_VERSION"
    testImplementation 'junit:junit:4.12'
}

task clearJar(type: Delete) {
    delete '../release/' + 'growthbeat-' + VERSION_NAME + '.jar'
}

task makeJar(type: Copy) {
    from('./build/intermediates/packaged-classes/release/')
    into('../release')
    include('classes.jar')
    rename('classes.jar', 'growthbeat-' + VERSION_NAME + '.jar')
}

makeJar.dependsOn(clearJar, build);

apply from: 'android-artifacts.gradle'

mavenPublishing {
    coordinates(GROUP, ARTIFACT_ID, VERSION_NAME)

    pom {
        name = ARTIFACT_ID
        description = POM_DESCRIPTION
        url = POM_URL
        licenses {
            license {
                name = POM_LICENCE_NAME
                url = POM_LICENCE_URL
                distribution = POM_LICENCE_DIST
            }
        }
        developers {
            developer {
                name = POM_DEVELOPER_NAME
                email = POM_DEVELOPER_EMAIL
                organization = POM_DEVELOPER_ORAGANIZATION
                organizationUrl = POM_DEVELOPER_ORAGANIZATION_URL
            }
        }
        scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
        }
    }

    signAllPublications()
}